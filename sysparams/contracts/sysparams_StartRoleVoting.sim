contract sysparams_StartRoleVoting{
    data{
        Id int
        VotingParamId int
        Start string "date"
        StartTime "date"
        End string "date"
    }
    
    func conditions{
        $votingParams = DBFind("sysparams_votings").WhereId($VotingParamId).Row()
        if $votingParams == nil{
            warning "Voting params not found"
        }
        
        $masterRoleName = $votingParams["master_role"]
        $candidateRoleName = $votingParams["candidate_role"]
        $votersRoleName = $votingParams["voters"]
        $votingName = $votingParams["title"]
        
        
        $masterRoleId = DBFind("roles_list").Where("delete=0 and role_name=?", $masterRoleName).One("id")
        if $masterRoleId == nil{
            warning "Master role not found"
        }
        
        $isMaster = DBFind("roles_assign").Where("delete=0 and member_id=? and role_id=?", $key_id, $masterRoleId).Row()
        if !$isMaster {
            info "You can not start to voting"
        }
        
        // Delegate to Validators voting : Голосование экосистем за валидаторов
        if $VotingParamId == 1{
            // renew Delegates
            $candidate = DBFind("sysparams_candidate").Where("id=?",$Id).Row()
            if !$candidate{
                warning "Candidate not found"
            }
            $KeyId = Int($candidate["candidate_key_id"])
            $desc = Sprintf("%v [%v]", $votingName, IdToAddress($KeyId))
            $isCreated = DBFind("voting_instances").Where("name=? and description=? and delete=0", $votingName, $desc).Row()
            if $isCreated{
                info "Voting already created"
            }
            
            sysparams_DelegateRefresh()
        }
        
        // Investor voting for Main Ecos : Голосование инвесторов за значимые экосистемы
        if $VotingParamId == 2{
            
            
        }
        
        $contractAccept = Sprintf("sysparams_AcceptRole(`ParamId,KeyId`, %v, %v)",$votingParams["id"], $KeyId)
    }
    
    func action{
        var typeDecision, typeParticipants, typeVoting, volume, quorum, vacancies, roleId int
        var role, candidate map
        // start voting for Candidate with Params = 50%
        volume = 50
        quorum = 50
        typeVoting = 2 // system
        typeParticipants = 3 // voting on Role
        typeDecision = 2 // Role candidates with manual registration members
        vacancies = 1
        
        role = DBFind("roles_list").Where("role_name=? and delete=0", $votersRoleName).Row()
        roleId = Int(role["id"])
        
        $voting_id = 0 // will be set in voting_CreateNew
        voting_CreateNew("voting_name,type_voting,description,type_participants,type_decision,now_date,start_time,start_date,end_date,end_time,volume,quorum", $votingName, typeVoting, $desc, typeParticipants, typeDecision, $Start, $StartTime, $Start, $End, "23:59:59", volume, quorum)
        if $voting_id == 0{
            $voting_id = DBFind("voting_instances").Where("voting_name=? and start_date=?", $votingName, $Start).One("id")
            $voting_id = Int($voting_id)
        }
        // after success voting set Validator Role to Candidate
        voting_SubjectCandidates("votingID,memberID", $voting_id, $KeyId)
        voting_SubjectSettings("votingID,roleID,vacancies,contract_reject,contract_accept", $voting_id, roleId, vacancies, "", $contractAccept)
        voting_Invite("votingID,var_id", $voting_id, roleId)
        
    }
}