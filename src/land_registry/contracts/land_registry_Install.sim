contract land_registry_Install{
    data{}
    
    func conditions{
        MainCondition()
    }
    
    func action{
        var members array
        members = DBFind("members")
        
        var i int
        while i< Len(members){
            var m map
            m = members[i]
            var amount int
            amount = 1000000
            var mAccount map
            mAccount = DBFind("land_registry_accounts").Where("member_id=?", m["id"]).Row()
            if mAccount {
                DBUpdate("land_registry_accounts", Int(mAccount["id"]), "member_id,member_avatar,member_name,amount_money,amount,onhold", Int(m["id"]),m["avatar"],m["member_name"], amount, amount,0)
            }else{
                DBInsert("land_registry_accounts", "member_id,member_avatar,member_name,amount_money,amount", Int(m["id"]),m["avatar"],m["member_name"], amount, amount)
            }
            i=i+1
        }
        // create required roles
        i = 0
        while i < 3{
            var roleName string
            roleName = Sprintf("depart_role%v", i+1)
            var role map
            role = DBFind("roles_list").Where("role_name=? and delete=0", roleName).Row()
            if !role{
                // role_type==1 must be assigned_role
                Roles_Create("role_name,role_type,company_id", roleName, 1, 0)
            }
            i=i+1
        }
        // assign roles
        i = 0
        while i < 3{
            var roleName string
            roleName = Sprintf("depart_role%v", i+1)
            var role, isAssigned map
            role = DBFind("roles_list").Where("role_name=? and delete=0", roleName).Row()
            
            // all roles to admin
            isAssigned = DBFind("roles_assign").Where("role_id=? and delete=0", role["id"]).Row()
            if !isAssigned{
                var j int
                if i < Len(members){
                    j = i
                }
                var m map
                m = members[j]
                Roles_Assign("role_id,member_id", Int(role["id"]), $key_id)
            }
            i=i+1
        }
    }
}