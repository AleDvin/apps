contract voting_UpdateDataForGraphs {
    data {
        votingID int
    }
    func notZero(num int) int {
        if num == 0 {
            num = 1
        }
        return num
    }
    conditions {
        $voting_map = DBFind("votings").Where("id=?", $votingID).Row()
        if !$voting_map {
            warning "Voting is not found"
        }
    }
    
    action {
        var progress_json, voting_json, flags_json string
        progress_json = Str($voting_map["progress"])
        voting_json = Str($voting_map["voting"])
        flags_json = Str($voting_map["flags"])
        
        var progress, voting, flags map
        progress = JSONToMap(progress_json)
        voting = JSONToMap(voting_json)
        flags = JSONToMap(flags_json)
        
        var number_participants, number_voters, percent_voters, percent_success, volume, quorum, flag_success int
        number_participants = Int(progress["number_participants"])
        number_voters = Int(progress["number_voters"])
        percent_voters = Int(progress["percent_voters"])
        volume = Int(voting["volume"])
        quorum = Int(voting["quorum"])
        
        // increment count of voters
        number_voters = number_voters + 1
        
        percent_voters = (number_voters * 100) / notZero(number_participants)
        if percent_voters > 100 {
            percent_voters = 100
        }
        
        percent_success = (percent_voters * 100) / notZero(volume)
        if percent_success > 100 {
            percent_success = 100
        }
        // update the flag of success
        if percent_success == 100 {
            flag_success = 1
        }
        
        progress["percent_success"] = Str(percent_success)
        progress["number_voters"] = Str(number_voters)
        progress["percent_voters"] = Str(percent_voters)
        
        flags["success"] = Str(flag_success)
        
        DBUpdate("votings", $votingID, "progress,flags", progress, flags)
        
        //if percent_voters >= quorum {
        // voting_CheckDecision("votingID", $votingID)
        //}
    }
}