contract Roles_Assign {
    data {
        role_id int
        member_id int
    }

    conditions {
        $role_map = DBFind("roles").Where("id = $", $role_id).Row()
        $member_map = DBFind("members").Where("id = $", $member_id).Row()
        $appointer_map = DBFind("members").Where("id = $", $key_id).Row()

        if !$role_map {
            warning "Role not found"
        }
        if !$member_map {
            warning "Member not found"
        }
        if !$appointer_map {
            warning "Appointer not found"
        }

        if (Int($role_map["deleted"]) == 1) {
            warning "This role has been deleted. The appointment of members is impossible"
        }

        if ((Int($role_map["role_type"]) != 1) && (Int($role_map["role_type"]) != 4)) {
            warning "This role is not assignable. The appointment of members is impossible"
        }

        // get JSON
        var creator_json string
        creator_json = Str($role_map["creator"])

        // get MAP
        var creator map
        creator = JSONToMap(creator_json)

        // check creator of the role
        if (Int(creator["member_id"]) != $key_id) {
            warning "Sorry, you are not the creator of this role. The appointment of members is impossible"
        }

        $assign_id = DBFind("roles_participants").Where("role->id=$ and member->member_id=$ and deleted=0", $role_id, $member_id).One("id")
        if $assign_id {
            warning "This member has already been added"
        }
    }

    action {
        var role map
        role["id"] = Str($role_map["id"])
        role["type"] = Str($role_map["role_type"])
        role["name"] = Str($role_map["role_name"])
        role["image_id"] = Str($role_map["image_id"])

        var member map
        member["member_id"] = Str($member_map["id"])
        member["member_name"] = Str($member_map["member_name"])
        member["image_id"] = Str($member_map["image_id"])

        var appointed map
        appointed["member_id"] = Str($appointer_map["id"])
        appointed["member_name"] = Str($appointer_map["member_name"])
        appointed["image_id"] = Str($appointer_map["image_id"])

        DBInsert("roles_participants", "role,member,appointed,timestamp date_created", role, member, appointed, $block_time)
    }
}