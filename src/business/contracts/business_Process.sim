contract business_Process {
    data {
        Id int // business_models:id
        ModelId int // business_models:model_id
        EventId int
        ResultId int
        ObjectType int
        ObjectId int
        Comment string "optional"
        NotificId string "optional"
    }
    func stringInArray(s string, a array) bool{
        var i int
        while i<Len(a){
            if a[i] == s{
                return true
            }
            i=i+1
        }
        return false
    }

    func updateObjectStatus(){
        var tables results manager_ids array
        tables = Split(AppParam($ApplicationId, "object_status_tables"), ",")
        results = Split(AppParam($ApplicationId, "results"), ",")
        manager_ids = Split(AppParam($ApplicationId, "manager_role_ids"), ",")
        if Len(tables) >= $ObjectType{
            var table string
            table = Replace(tables[$ObjectType-1], " ", "")
            if Size(table) > 1{
                if DBFind(table).WhereId($ObjectId).One("status"){
                    var res status string
                    res = results[$ResultId-1]
                    if res == "Accept" && stringInArray(Str($role_id), manager_ids){
                        res = "AM Accept"
                    }
                    status = $STATUSES[res]
                    if status {
                        DBUpdate(table, $ObjectId, "status", status)
                    }
                }else{
                    warning "Status not found"
                }
            }
        }
    }

    conditions {
        var statuses map
        //0 Not Submitted
        statuses["Submit"] = 1 //"Submitted"
        statuses["Escalate"] = 2 //"Escalated"
        statuses["Send Back"] = 3 //"Send Back"
        statuses["Accept"] = 4 //"Accepted"
        statuses["Pay"] = 5 // "Paid"
        statuses["AM Accept"] = 6 // "AM Approved"
        statuses["Reject"] = 7 //"Rejected"
        $STATUSES = statuses

        $ApplicationId = Int(DBFind("applications").Where("name='Business'").One("id"))
        if $ApplicationId == 0 {
            warning "Application ID not found"
        }
        $numSubmitted = Int(DBFind("business_events").Where("event_id=? and object_type=? and object_id=? and key_id=? and result_id=? and model_id=?", $EventId, $ObjectType, $ObjectId, $key_id, $ResultId, $ModelId).Order("num_submitted desc").One("num_submitted")) + 1
        $models = DBFind("business_models").Order("event_id").Where("deleted='0' and model_id=?", $ModelId)

        var currentModel map
        currentModel = DBFind("business_models").Where("deleted='0' and id=?", $Id).Row()
        if !currentModel{
            error "Business model not found"
        }
        if currentModel["need_comment"] == 1 && Size($Comment) < 1 {
            warning "Need comment"
        }
        $NotificId = Int($NotificId)
    }
    action {
        DBInsert("business_events", "event_id,object_type,object_id,key_id,result_id,comment,num_submitted,created_at,model_id", $EventId, $ObjectType, $ObjectId, $key_id, $ResultId, $Comment, $numSubmitted, "Now()", $ModelId)

        updateObjectStatus()

        var i int
        var nextModel map
        var isNextFound bool
        while i<Len($models){
            nextModel = $models[i]
            var cond map
            cond = JSONDecode(nextModel["condition"])
            if cond["0"] == $ResultId || cond[Str($EventId)] == $ResultId {
                isNextFound = true
                break
            }
            i=i+1
        }
        if $NotificId > 0{
            Notifications_Roles_Processing("notific_id", $NotificId)
            Notifications_Roles_Close("notific_id", $NotificId)
        }
        if isNextFound {
            var roleId closureType sender int
            roleId = nextModel["role_id"]
            closureType = 2 // multiple
            sender = 2 // from admin?
            var icon header body page string
            icon = "icon-envelope"
            header = "Business process: Next step"
            body = nextModel["title"]
            page = "business_process"
            var params map
            params["id"] = nextModel["id"]
            params["object_id"] = $ObjectId
            params["comment"] = $Comment
            Notifications_Roles_Send_map("rid,closure_type,sender,icon_name,text_header,text_body,page_name,params_map", roleId, closureType, sender, icon, header, body, page, params)

        }else{
            Println("Next step not found")
        }
    }
}