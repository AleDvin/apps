contract voting_TemplateStore{
    data{
        Title string
        TypeParticipants int
        TypeVoting int
        TypeDecision int
        Volume int
        Quorum int
        Subject string
        Voters string
        InitContract string "optional" // Выполнить перед голосованием
        Vacancies int "optional"
        ContractAccept string "optional"
        ParamsAccept string "optional"
        ContractReject string "optional"
        ParamsReject string "optional"
    }
    
    func conditions{
        if Size($InitContract) > 0{
            if !IsObject($InitContract, $ecosystem_id){
                warning "Init condition not found"
            }
        }
        
        if Size($Title) <3{
            warning "Please enter title"
        }
        if $Volume <0 || $Volume > 100{
            warning "Wrong volume range"
        }
        if $Quorum <0 || $Quorum > 100{
            warning "Wrong quorum range"
        }
        
        if $TypeDecision != 5 && $Vacancies < 1 {
            warning "wrong number vacancies"
        }
        
        if Size($ContractAccept) > 0{
            if GetContractByName($ContractAccept) == 0 {
                warning "Contract for accepted decision not found"
            }
        }
        if Size($ContractReject) > 0{
            if GetContractByName($ContractReject) == 0{
                warning "Contract for rejected decision not found"
            }
        }
    }
    
    func action{
        DBInsert("voting_templates", "title,subject,voters,init_contract,volume,quorum,type_voting,type_participants,type_decision,vacancies,contract_accept,accept_params,contract_reject,reject_params", $Title,$Subject,$Voters,$InitContract,$Volume,$Quorum,$TypeVoting,$TypeParticipants,$TypeDecision,$Vacancies,$ContractAccept,$ParamsAccept,$ContractReject,$ParamsReject)
        
    }
}