contract test_voting_Create{
    data{
        Start string "datetime"
        End string "datetime"
        StartTime string "datetime"
        EndTime string "datetime"
    }
    
    action{
        var volume, quorum, vacancies int
        volume = 50
        quorum = 50
        vacancies = 1
        
        var SUBJ, voters, candidates, INIT, ACCEPT, ACCEPT_PARAMS, REJECT, REJECT_PARAMS string
        candidates = DBFind("roles").Where("deleted=0 and (role_type=2 or role_type=3)").One("id")
        if !candidates{
            warning "Candidate role not found. Please create role with type of 2 or 3 for tests"
        }
        
        voters = DBFind("roles").Where("deleted=0").One("id")
        if !voters {
            warning "Voter role not found"
        }
        SUBJ = "voting template subject"
        INIT = "test_voting_Init"
        ACCEPT = "test_voting_Accept"
        ACCEPT_PARAMS = ""
        REJECT = "test_voting_Reject"
        REJECT_PARAMS = ""
        
        var p, d, v int // types of participant, decision, voting
        d = 1
        while d <= 5 {
            p = 1
            while p <= 3 {
                if p==2 {
                    // passed with manually entering participants
                    p=p+1
                    continue
                }
                v = 1
                while v <= 2 {
                    var title string
                    title = Sprintf("New Voting d%v:p%v:v%v", d, p, v)
                    
                    var templateId, kid int
                    voting_TemplateStore("Title,TypeParticipants,TypeVoting,TypeDecision,Volume,Quorum,Subject,Voters,Candidates,InitContract,Vacancies,ContractAccept,ParamsAccept,ContractReject,ParamsReject", title,p,v,d,volume,quorum,SUBJ,voters,candidates,INIT,vacancies,ACCEPT,ACCEPT_PARAMS,REJECT,REJECT_PARAMS)
                    templateId = $result
                    
                    var tableWritten, rowId, columnName, newValue, textDocument string
                    tableWritten = "debug"
                    rowId = "1"
                    columnName = "value1"
                    newValue = Sprintf("It is new value for template #%v", templateId)
                    textDocument = Sprintf("Test text Document for voting template #%v", templateId)
                    
                    voting_TemplateRun("TemplateId,Start,End,EndTime,StartTime,KeyId,TableWritten,RowId,ColumnName,NewValue,TextDocument", templateId, $Start, $End, $EndTime, $StartTime, kid, tableWritten, rowId, columnName, newValue, textDocument)
                    
                    v = v+1
                }
                p = p+1
            }
            d = d+1
        }
    }
}