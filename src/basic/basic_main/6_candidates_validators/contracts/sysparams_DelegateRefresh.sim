contract sysparams_DelegateRefresh{
    func NotInArray(val string, arr array) bool{
        var i int
        // info arr
        while i< Len(arr){
            if val == arr[i]{
                return false
            }
            i=i+1
        }
        return true
    }
    
    conditions{
        var roleName string
        var roleId int
        roleName = "Delegate"
        roleId = 5
        $delegateParam = "delegate_account"
        
        // get role Id and Type
        $role = DBFind("roles_list").Where("role_name=? and id=? and delete=0", roleName, roleId).Row()
        if !$role{
            return "Role not found"
        }
    }
    
    action{
        $origin_contract = "sysparams_DelegateRefresh"
        // get delegates ids
        var delegateIds array
        var dIndex, i, lastEcosystemNumber int
        i = 1
        lastEcosystemNumber = 15
        while i <= lastEcosystemNumber {
            $delegate = DBFind("parameters").Where("name=?",$delegateParam).Ecosystem(i).Row()
            if !$delegate{
                $delegate = DBFind("parameters").Where("name='founder_account'").Ecosystem(i).Row()
            }
            $delegateValue = $delegate["value"]
            if NotInArray($delegateValue, delegateIds){
                delegateIds[dIndex] = $delegateValue
                dIndex = dIndex + 1
            }
            i = i + 1
        }
        
        // remove old role assigns
        $currentDelegates = DBFind("roles_assign").Where("role_id=? and delete=0", $role["id"])
        i = 0
        while i < Len($currentDelegates){
            $currentDelegate = $currentDelegates[i]
            if NotInArray($currentDelegate["member_id"], delegateIds){
                var id int
                id = Int($currentDelegate["id"])
                Roles_Unassign("row_id", id)
            }
            i = i + 1
        }
        
        // assign new roles
        i = 0
        while i < Len(delegateIds) {
            var delegateId int
            delegateId = Int(delegateIds[i])
            var member map
            member = DBFind("members").Where("id=?",delegateId).Row()
            if member{
                // check role dup
                var assigned map
                assigned = DBFind("roles_assign").Where("member_id=? and role_id=? and delete=0", delegateId, $role["id"]).Row()
                if !assigned{
                    Roles_Assign("role_id,member_id", Int($role["id"]), Int(member["id"]))
                }
            }
            i=i+1
        }
    }
}