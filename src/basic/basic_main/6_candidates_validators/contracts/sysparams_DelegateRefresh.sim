contract sysparams_DelegateRefresh{
    func NotInArray(val string, arr array) bool{
        var i int
        // info arr
        while i< Len(arr){
            if val == arr[i]{
                return false
            }
            i=i+1
        }
        return true
    }
    
    func conditions{
        $roleName = "Delegate"
        $delegateParam = "delegate_account"
        
        // get role Id and Type
        $role = DBFind("roles_list").Where("role_name=?", $roleName).Order("id").Row()
        if !$role{
            return "Role not found"
        }
    }
    
    func action{
        // get delegates ids
        var delegateIds array
        var dIndex int
        $i = 1
        while $i <= $ecosystem_id {
            $delegate = DBFind("parameters").Where("name=?",$delegateParam).Ecosystem($i).Row()
            if !$delegate{
                $delegate = DBFind("parameters").Where("name='founder_account'").Ecosystem($i).Row()
            }
            $delegateValue = $delegate["value"]
            if NotInArray($delegateValue, delegateIds){
                delegateIds[dIndex] = $delegateValue
                dIndex = dIndex + 1
            }
            $i = $i + 1
        }
        
        // remove old role assigns
        $currentDelegates = DBFind("roles_assign").Where("role_id=? and delete=0", $role["id"])
        $i = 0
        while $i < Len($currentDelegates){
            $currentDelegate = $currentDelegates[$i]
            if NotInArray($currentDelegate["member_id"], delegateIds){
                $Id = $currentDelegate["id"]
                DBUpdate("roles_assign", Int($Id), "delete", 1)
            }
            $i = $i + 1
        }
        
        // assign new roles
        var i int
        while i < Len(delegateIds) {
            var delegateId int
            delegateId = Int(delegateIds[i])
            var member map
            member = DBFind("members").Where("id=?",delegateId).Row()
            if member{
                // check role dup
                var assigned map
                assigned = DBFind("roles_assign").Where("member_id=? and role_id=? and delete=0", delegateId, $role["id"]).Row()
                if !assigned{
                    sysparams_RoleAssign("RoleName,KeyId", $roleName, Int(member["id"]))
                }
            }
            i=i+1
        }
    }
}