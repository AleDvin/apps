contract sysparams_StartNodeRemove{
    data{
        Index int
        Duration int
    }
    conditions{
        var appid int
        appid = Int(DBFind("applications").Columns("name,id").Where("name='Basic application'").One("id"))
        $templateId = Int(AppParam(appid, "voting_sysparams_template_id"))
        if $templateId == 0 {
            warning "Template id not found"
        }
    }

    action{
        var nodesJSON string nodes newNodes empty array emptyNode map
        nodesJSON = SysParamString("full_nodes")
        if Size(nodesJSON) > 100 {
            nodes = JSONDecode(nodesJSON)
            var i int
            while i < Len(nodes){
                var node map
                node = nodes[i]
                if i != $Index {
                    newNodes = Append(newNodes, node)
                }
                i=i+1
            }
        }
        if Len(newNodes) <= 1 {
            // TODO запихать какое то валидное пустое значение в список нод
            emptyNode["api_address"] = ""
            emptyNode["key_id"] = ""
            emptyNode["public_key"] = ""
            emptyNode["tcp_address"] = ""
            empty[0] = emptyNode
            newNodes = empty
        }
        voting_TemplateRun("TemplateId,Duration,KeyId,TableWritten,RowId,ColumnName,NewValue,TextDocument", $templateId, $Duration, 0, "", "", "full_nodes", JSONEncode(newNodes), "")
    }
}