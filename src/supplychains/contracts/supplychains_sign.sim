contract supplychains_sign {
    data {
        step_id int
        vFlag int
        photo string "image"
        notific_id int
    }

    conditions {}

    action {

        if ($vFlag == 1) {
            DBUpdateExt("supplychains_steps", "id", $step_id, "holder_signature, timestamp holder_date_signature", $key_id, $block_time)
        } else {
            DBUpdateExt("supplychains_steps", "id", $step_id, "receiver_signature, timestamp receiver_date_signature,photo", $key_id, $block_time, $photo)
        }

        var supply_steps array
        supply_steps = DBFind("supplychains_steps").Columns("holder_signature,receiver_signature,supply_id ").Where("id=?", $step_id)

        var supply_step map
        supply_step = supply_steps[0]

        var holder_signature int
        holder_signature = supply_step["holder_signature"]

        var receiver_signature int
        receiver_signature = supply_step["receiver_signature"]


        if (holder_signature != 0 && receiver_signature != 0) {
            var supply_id int
            supply_id = supply_step["supply_id"]

            var supplies array
            supplies = DBFind("supplychains_supply").Columns("step,total_steps").Where("id=?", supply_id)

            //DBInsert("debug", "data", "52")

            var supply map
            supply = supplies[0]

            var step int
            step = supply["step"]

            var total_steps int
            total_steps = supply["total_steps"]

            step = step + 1

            if step >= Int(Str(total_steps)) {
                DBUpdateExt("supplychains_supply", "id", supply_id, "step,status", step, 4)
            } else {
                DBUpdateExt("supplychains_supply", "id", supply_id, "step", step)
                supplychains_send_notifics("supply_id", supply_id)
            }
        }


        if ($notific_id > 0) {
            Notifications_Single_Close("notific_id", $notific_id)
        }

    }
}