contract NewLang {
    data {
        ApplicationId int "optional"
        Name string
        Trans string "optional"
        Value array "optional"
        IdLanguage array "optional"
    }

    conditions {
        if $ApplicationId == 0 {
            warning "Application id cannot equal 0"
        }

        if DBFind("languages").Columns("id").Where("name = ?", $Name).One("id") {
            warning Sprintf( "Language resource %s already exists", $Name)
        }

        var j int
        while j < Len($IdLanguage) {
            if $IdLanguage[j] == "" {
                info("Locale empty")
            }
            if $Value[j] == "" {
                info("Value empty")
            }
            j = j + 1
        }
        EvalCondition("parameters", "changing_language", "value")
    }

    action {
        if Len($IdLanguage) > 0 {
            var trans map
            var i int
            while i < Len($IdLanguage) {
                var lang, val string
                lang = $IdLanguage[i]
                val = $Value[i]
                trans[lang] = val
                i = i + 1
            }
            $Id = DBInsert("languages", "name,res,app_id", $Name, trans, $ApplicationId)
        } else {
            $Id = DBInsert("languages", "name,res,app_id", $Name, $Trans, $ApplicationId)
        }
    }
}