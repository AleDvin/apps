contract Import_New {
    data {
        Type string
        Name string "optional"
        Value string "optional"
        Conditions string "optional"
        Menu string "optional"
        Trans string "optional"
        Columns string "optional"
        Permissions string "optional"
        Title string "optional"
    }

    conditions {
        Println("Import: " + $Name)
        $ApplicationId = 0

        var app_map map
        app_map = DBFind("buffer_data").Columns("value->app_name").Where("key='import_info' and member_id=$", $key_id).Row()
        if app_map{
            var app_id int
            app_id = DBFind("applications").Columns("id").Where("name=$", Str(app_map["value.app_name"])).One("id")

            if app_id {
                $ApplicationId = Int(app_id)
            }
        }
    }

    action {
        var cdata, editors, creators, tables map
        cdata["Value"] = $Value
        cdata["Conditions"] = $Conditions
        cdata["ApplicationId"] = $ApplicationId
        cdata["Name"] = $Name
        cdata["Title"] = $Title
        cdata["Trans"] = $Trans
        cdata["Menu"] = $Menu
        cdata["Columns"] = $Columns
        cdata["Permissions"] = $Permissions

        editors["page"] = "EditPage"
        editors["block"] = "EditBlock"
        editors["menu"] = "EditMenu"
        editors["parameter"] = "EditAppParam"
        editors["language"] = "EditLang"
        editors["contract"] = "EditContract"
        editors["table"] = "" // nothing

        creators["page"] = "NewPage"
        creators["block"] = "NewBlock"
        creators["menu"] = "NewMenu"
        creators["parameter"] = "NewAppParam"
        creators["language"] = "NewLang"
        creators["contract"] = "NewContract"
        creators["table"] = "NewTable"

        tables["page"] = "pages"
        tables["block"] = "blocks"
        tables["menu"] = "menu"
        tables["parameter"] = "parameters"
        tables["language"] = "languages"
        tables["contract"] = "contracts"
        tables["table"] = "tables"

        var contractName string
        var item map
        item = DBFind(tables[$Type]).Where("name=?", $Name).Row()
        if item {
            contractName = editors[$Type]
            cdata["Id"] = Int(item["id"])
            if $Type == "menu" {
                cdata["Value"] = item["value"] + "\n" + $Value
            }
        } else {
            contractName = creators[$Type]
        }


        if contractName != ""{
            CallContract(contractName, cdata)
        }
    }
}