contract NewTableJoint {
    data {
        ApplicationId int
        Name string
        ColumnsArr array
        TypesArr array
        InsertPerm string
        UpdatePerm string
        NewColumnPerm string
    }

    conditions {
        if Len($ColumnsArr) == 1 && Contains($ColumnsArr[0], ","){
            $ColumnsArr = Split($ColumnsArr[0], ",")
        }
        if Len($TypesArr) == 1 && Contains($TypesArr[0], ","){
            $TypesArr = Split($TypesArr[0], ",")
        }

        var i int
        while i < Len($ColumnsArr) {
            if Size($ColumnsArr[i]) == 0 {
                info("Columns is empty")
            }
            if Size($TypesArr[i]) == 0 {
                info("Type is empty")
            }
            i = i + 1
        }
    }

    action {
        var i int cols array perms params map
        while i < Len($ColumnsArr) {
            var col map
            col["name"] = $ColumnsArr[i]
            col["type"] = $TypesArr[i]
            col["conditions"] = "true"
            cols[i] = JSONEncode(col)
            i = i + 1
        }

        perms["insert"] = $InsertPerm
        perms["update"] = $UpdatePerm
        perms["new_column"] = $NewColumnPerm

        params["ApplicationId"] = $ApplicationId
        params["Name"] = $Name
        params["Columns"] = JSONEncode(cols)
        params["Permissions"] = JSONEncode(perms)
        CallContract("NewTable", params)
    }
}