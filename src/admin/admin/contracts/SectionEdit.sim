contract SectionEdit {
    data {
		id int
		title string
		status int
		urlname string
		page string
		roles_access_ids array
		roles_access_vals array
    }
    conditions {
		if $status < 0 || $status > 2 {
			warning "Status must be in range 0..2"
		}
		if DBRow("sections").Columns("id").Where("urlname = ? and id != ?", $urlname, $id) {
			warning Sprintf("Section with url name '%s' already exists", $urlname)
		}
		if Len($roles_access_ids) != Len($roles_access_vals) {
			warning Sprintf("Given '%v' role IDs but checked '%v' role IDs", Len($roles_access_ids), Len($roles_access_vals))
		}
		if !DBRow("sections").Columns("id").WhereId($id) {
			warning Sprintf("Section with id '%v' not exists", $id)
		}
    }
    action {
		var roles_access string i, j int
		roles_access = "["
		while i < Len($roles_access_ids) {
			if $roles_access_vals[i] == "true" {
				if j > 0 {
					roles_access = roles_access + ","
				}
				roles_access = roles_access + Str($roles_access_ids[i])
				j = j + 1
			}
			i = i + 1
		}
		if $status == 2 && DBRow("sections").Columns("id").Where("status = ?", 2) {
			DBUpdateExt("sections", "status", "2", "status", "1")
		}
		DBUpdate("sections", $id, "title,status,urlname,page,roles_access", $title, $status, $urlname, $page, roles_access)
    }
}