contract land_registry_Add{
    data{
        Coords string
        LandUse int
        BuildingsUseClass int
        Area int
        CoordsAddress string
        OwnerId int
        RegistrationDate string
        LandNature int
        LandLocalBody int
    }
    
    func conditions{
        var member map
        member = DBFind("member").Where("id = ?", $OwnerId).Row()
        if !member {
            warning "Member account for Owner not found"
        }
        var rolesTbl string
        rolesTbl = "roles_list"
        var i int
        while i < 3{ // create required roles
            var role map
            var roleName, where string
            roleName = Sprintf("depart_role%v", i+1)
            role = DBFind(rolesTbl).Where("role_name=? and delete=0", roleName).Row()
            if !role{
                // role_type==1 must be assigned_role
                Roles_Create("role_name,role_type,company_id", roleName, 1, 0)
            }
            i=i+1
        }
        
        $depart2_RoleId = DBFind(rolesTbl).Where("role_name='depart_role2' and delete=0").One("id")
        $depart3_RoleId = DBFind(rolesTbl).Where("role_name='depart_role3' and delete=0").One("id")
        
    }
    func action{
        var icon, header, body, page, params, roleParams string
        var closureType, landId int
        
        landId = DBInsert("land_registry", "owner_id,address,area,buildings_use_class,map_coords,land_use,timestamp date_create,delete,land_nature,land_local_body,registration_date,timestamp modification_date", $OwnerId, $CoordsAddress, $Area, $BuildingsUseClass, $Coords, $LandUse, $block_time, 0, $LandNature, $LandLocalBody, $RegistrationDate, $block_time)
        
        DBInsert("land_registry_ownership", "step, timestamp date_creat, timestamp date_signing, lend_object_id, owner_id, owner_new_id", -1, $block_time, $block_time, landId, $OwnerId, $OwnerId)
        
        icon = "fa fa-bell-o"
        header = "New Land"
        body = "Need your processing"
        params = Sprintf("val_int=%v", landId)
        closureType = 2
        
        roleParams = "role_id,closure_type,icon_name,text_header,text_body,page_name,params_val"
        
        //notify role2
        page = "land_registry_edit_role2"
        Notifications_Roles_Send(roleParams,Int($depart2_RoleId),closureType,icon,header,body,page,params)
        
        //notify role3
        page = "land_registry_edit_role3"
        Notifications_Roles_Send(roleParams,Int($depart3_RoleId),closureType,icon,header,body,page,params)
        
        //notify owner
        page = "land_registry_edit_role1"
        Notifications_Single_Send("member_id,icon_name,text_header,text_body,page_name,params_val", $OwnerId,icon,header,body,page,params)
    }
}