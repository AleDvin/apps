contract land_registry_MoneyTransfer_NoSign{
    data{
        from_acc_id int
        to_acc_id int
        Amount money
    }
    
    func conditions{
        if $from_acc_id == $to_acc_id {
            warning "Sender account ID and Recipient account ID are the same."
        }
        
        //----CHECK RECIPIENT----------------------------
        var recipientAccount map
        recipientAccount = DBFind("land_registry_accounts").Where("id=?", $to_acc_id).Row()
        if !recipientAccount {
            warning "Recipient account not found"
        }
        if recipientAccount["onhold"] > 0 {
            warning "Recipient account on hold"
        }
        
        //----CHECKING SENDER----------------------------
        if $from_acc_id != 0 {
            var senderAccount map
            senderAccount = DBFind("land_registry_accounts").Where("id=?",$from_acc_id).Order("id desc").Row()
            if !senderAccount {
                warning "Sender account not found"
            }
            if Int(senderAccount["onhold"]) > 0 {
                warning "Sender account on hold"
            }
            
            $senderBalance = money(senderAccount["amount"])
            if $senderBalance < $Amount {
                warning "Sorry, your account has insufficient funds"
            }
        }
        else{
            // if senderAccount_id == 0, then this emission
            MainCondition()
        }
    }
    
    func action {
        var recipientAccount map
        recipientAccount = DBFind("land_registry_accounts").WhereId($to_acc_id).Row()
        var recipientBalance money
        recipientBalance = Int(recipientAccount["amount"]) + $Amount
        DBUpdate("land_registry_accounts", $to_acc_id, "amount", recipientBalance)
        
        if $from_acc_id != 0 {
            $senderBalance = $senderBalance - $Amount
            DBUpdate("land_registry_accounts", $from_acc_id, "amount", $senderBalance)
        }
    }
}