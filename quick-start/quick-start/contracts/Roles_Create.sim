contract Roles_Create {
    data {
        role_name string
        role_type int
        company_id string "optional"
    }
   func getMemberInfo(kid int) map {
        var res member map
        member = DBFind("members").Where("id=?", kid).Row()
        if member {
            res["member_id"] = member["id"]
            res["member_name"] = member["member_name"]
            res["image_id"] = member["image_id"]
        }else{
            member = DBFind("keys").Where("id=?", kid).Row()
            if member {
                res["member_id"] = member["id"]
                res["member_name"] = IdToAddress(Int(member["id"]))
                res["image_id"] = 0
            }
        }
        return res
    }
    conditions {
        $company_id = Int($company_id)
        // system role
        if $role_type == 3 {
            warning "System role can not be created"
        }

        // company role
        if $role_type == 4 {
            // check param
            if $company_id == 0 {
                warning "On this page you can not create a role for the company. Go to the company's view page to create a role"
            }

            // check creator of the company
            if $company_id != $key_id {
                warning "Sorry, you are not the creator of this company"
            }
        }
    }

    action {
        var creator map
        creator = getMemberInfo($key_id)

        if $role_type != 4 {
            $company_id = 0
        }
        $result = DBInsert("roles", "role_name,role_type,creator,timestamp date_created,company_id", $role_name, $role_type, creator, $block_time, $company_id)
    }
}